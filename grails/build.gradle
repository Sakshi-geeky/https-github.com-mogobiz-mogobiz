buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.mogobiz.gradle:groovy-eclipse-compiler:1.0.1-SNAPSHOT"
        classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:0.5.1'
        classpath "org.grails:grails-gradle-plugin:2.1.1"
    }
}

allprojects {
    group "com.mogobiz"
    version "1.0.1-SNAPSHOT"
    apply plugin: "maven"
    apply plugin: "maven-publish"
    repositories {
        mavenLocal()
        mavenCentral()
    }
    publish.dependsOn([])
}

ext {
    jacksonVersion = '2.7.0'
    oauth2Version = '0.31'
    twitter4jVersion = '2.2.5'
    groovyVersion = '2.4.10'
    binaryPlugin = false
}

configure(
    subprojects.findAll{
        it.name.startsWith('plugins/grails-') ||
        it.name == 'mogobiz-core' ||
        it.name == 'mogobiz-extensions'
    }
    ) {

    apply plugin: "grails"

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://repo.grails.org/grails/core"
            url "http://repo.grails.org/grails/plugins"
        //    url "https://art.ebiznext.com/artifactory/libs-release/"
        }
        grails.central() //creates a maven repo for the Grails Central repository (Core libraries and plugins)
    }

    grails {
        grailsVersion = '2.5.6'
        groovyVersion = '2.4.10'
        springLoadedVersion '1.2.0.RELEASE'
    }

    dependencies {
        test "org.grails:grails-test:${grailsVersion}"
        test "org.grails:grails-plugin-testing:${grailsVersion}"
        test 'org.spockframework:spock-core:1.1-groovy-2.4'
        test 'junit:junit:4.12'
        provided 'org.grails.plugins:tomcat:7.0.54'
    }

    packagePlugin.outputs.upToDateWhen { 
        if(binaryPlugin){
            project.file("target/grails-plugin-${project.archivesBaseName.minus('grails-plugin-')}-${project.version}.jar").exists() 
        }
        else{
            project.file("grails-${project.archivesBaseName.minus('grails-')}-${project.version}.zip").exists() 
        }
    }

    clean.doLast{
        if(binaryPlugin){
            project.file("target/grails-plugin-${project.archivesBaseName.minus('grails-plugin-')}-${project.version}.jar").delete() 
        }
        else{
            project.file("grails-${project.archivesBaseName.minus('grails-')}-${project.version}.zip").delete() 
        }
    }

    publishToMavenLocal.dependsOn([packagePlugin])

    task install(description: "Installs the grails plugin into the local Maven repository.", dependsOn: [packagePlugin, publishToMavenLocal])

    task deploy(description: "Deploys the grails plugin into the remote Maven repository.", dependsOn: [install, publish])

    publishing {
        publications{
            grailsPlugin(MavenPublication) {
                artifactId = "${project.archivesBaseName}" 
                if(binaryPlugin){
                    artifact("target/grails-plugin-${project.archivesBaseName.minus('grails-plugin-')}-${project.version}.jar"){
                        extension "jar"
                        classifier "grails-plugin"
                    }
                }
                else{
                    artifact("grails-${project.archivesBaseName.minus('grails-')}-${project.version}.zip"){
                        extension "zip"
                    }
                }
            }
        }
        repositories {
            maven {
                // TODO replace with remote repository
                url "$buildDir/repo"
            }
        }
    }

}
