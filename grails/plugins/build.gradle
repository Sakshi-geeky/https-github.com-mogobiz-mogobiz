buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:2.1.1"
    }
}

version "1.0.1-SNAPSHOT"

apply plugin: "java"

allprojects {
    group "com.mogobiz"
    apply plugin: "maven"
    apply plugin: "maven-publish"
}

subprojects {

    apply plugin: "grails"

    repositories {
        mavenCentral()
        maven {
            url "http://repo.grails.org/grails/core"
            url "http://repo.grails.org/grails/plugins"
        //    url "https://art.ebiznext.com/artifactory/libs-release/"
        }
        grails.central() //creates a maven repo for the Grails Central repository (Core libraries and plugins)
    }

    grails {
        grailsVersion = '2.5.6'
        groovyVersion = '2.4.10'
        springLoadedVersion '1.2.0.RELEASE'
    }

    dependencies {
        test "org.grails:grails-test:${grailsVersion}"
        test "org.grails:grails-plugin-testing:${grailsVersion}"
        test 'org.spockframework:spock-core:1.1-groovy-2.4'
        test 'junit:junit:4.12'
        provided 'org.grails.plugins:tomcat:7.0.54'
    }

    packagePlugin.outputs.upToDateWhen { false }

    task install(description: "Installs the grails plugin into the local Maven repository.", dependsOn: [packagePlugin, publishToMavenLocal])

    task deploy(description: "Deploys the grails plugin into the remote Maven repository.", dependsOn: [install, publish])

    publishing {
        publications{
            grailsPlugin(MavenPublication) {
                artifactId = "${project.archivesBaseName}" 
                artifact("${project.name}-${project.version}.zip"){
                    extension "zip"
                }
            }
        }
        repositories {
            maven {
                // TODO replace with remote repository
                url "$buildDir/repo"
            }
        }
    }

}
